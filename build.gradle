plugins {
	id "com.jfrog.bintray" version "1.8.4"
}

repositories {
	jcenter()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
version = "0.4.1"

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
	afterTest { desc, result ->
		println "Test ${desc.name} [${desc.className}] result: ${result.resultType}"
	}
}

def pomConfig = {
	licenses {
		license {
			name 'The MIT License'
			url 'https://opensource.org/licenses/MIT'
			distribution 'repo'
		}
	}
	developers {
		developer {
			id 'fornwall'
			name 'Fredrik Fornwall'
			email 'fredrik@fornwall.net'
		}
	}
	scm {
		url "https://github.com/fornwall/jelf"
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

// See https://github.com/bintray/gradle-bintray-plugin
publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId 'net.fornwall.jelf'
			artifactId 'jelf'
			version this.version
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'ELF parsing library in java')
				root.appendNode('name', 'JElf')
				root.appendNode('url', 'https://github.com/fornwall/jelf')
				root.children().last() + pomConfig
			}
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['MyPublication']
	pkg {
		repo = 'maven'
		userOrg = user
		name = 'jelf'
		labels = ['java', 'elf']
		version {
			gpg {
				sign = true
				passphrase = property('gpg.passphrase')
			}
		}
	}
}
